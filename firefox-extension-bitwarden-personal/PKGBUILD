# Maintainer: Matt Harrison <matt@harrison.us.com>
# Contributor: David Hummel <hummeltech@sherpaguru.com>
# Maintained at: https://github.com/matt-h/aur-pkgbuilds or https://codeberg.org/matt/aur-pkgbuilds

pkgname=firefox-extension-bitwarden-personal
pkgver=2026.1.1
pkgrel=1
pkgdesc='Bitwarden browser extension for Firefox - Matt Edition'
arch=('any')
url='https://github.com/bitwarden/clients'
license=('GPL-3.0-or-later')
optdepends=('firefox')
groups=('firefox-addons')
pkgdesc='Bitwarden browser extension for Firefox'
makedepends=('nodejs-lts-krypton' 'npm' 'unzip' 'imagemagick' 'perl' 'ttf-droid' 'zip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bitwarden/clients/archive/refs/tags/browser-v${pkgver}.tar.gz")
b2sums=('e081f2eda91bf6b0757d8a96fb264a5a099d6d3fbd05140e2d838b41b331a52728219a82782e6f9ef4cb636c5eaa5f7d3259107807fc16a01f91198cc88093e0')

prepare() {
  cd "${srcdir}/clients-browser-v${pkgver}" || exit
  npm ci
}

build() {
  cd "${srcdir}/clients-browser-v${pkgver}/apps/browser" || exit
  find . -type f -exec perl -pi -e 's/"Bitwarden"/"Bitwarden - Matt"/go;' {} \;
  find . -type f -exec perl -pi -e 's/"Bitwarden - Free Password Manager"/"Bitwarden - Matt"/go;' {} \;
  find . -type f -exec perl -pi -e 's/"Bitwarden Password Manager"/"Bitwarden - Matt"/go;' {} \;
  find . -type f -exec perl -pi -e 's/446900e4-71c2-419f-a6a7-df9c091e268b/37c30bda-db14-4b06-aeb1-a1998de1a766/go;' {} \;
  find . -type f -exec perl -pi -e 's{<title>Bitwarden</title>}{<title>Bitwarden - Matt</title>}go;' {} \;
  for file in src/images/*; do
    # if file is not png, skip it
    if [[ $file != *.png ]]; then
      echo "Skipping non-png file: $file"
      continue
    fi
    # if file doesn't start with icon, skip
    if [[ $(basename "$file") != icon* ]]; then
      echo "Skipping non-icon file: $file"
      continue
    fi

    # change color of the icon to green
    echo "Updating icon color: $file"
    magick "$file" -modulate 100,70,60 "$file"
  done
  npm run dist:firefox
}

check() {
  cd "${srcdir}/clients-browser-v${pkgver}/apps/browser" || exit
  npm run test
}

package() {
  install -Dm644 "${srcdir}/clients-browser-v${pkgver}/apps/browser/dist/dist-firefox.zip" "${pkgdir}/usr/lib/firefox/browser/extensions/{37c30bda-db14-4b06-aeb1-a1998de1a766}.xpi"
}
