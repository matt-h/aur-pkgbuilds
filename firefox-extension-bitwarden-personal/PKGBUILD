# Maintainer: Matt Harrison <matt@harrison.us.com>
# Contributor: David Hummel <hummeltech@sherpaguru.com>
# Maintained at: https://github.com/matt-h/aur-pkgbuilds or https://codeberg.org/matt/aur-pkgbuilds

pkgname=firefox-extension-bitwarden-personal
pkgver=2025.1.1
pkgrel=1
pkgdesc='Bitwarden browser extension for Firefox - Matt Edition'
arch=('any')
url='https://github.com/bitwarden/clients'
license=('GPL-3.0-or-later')
optdepends=('firefox')
groups=('firefox-addons')
pkgdesc='Bitwarden browser extension for Firefox'
makedepends=('nodejs' 'npm' 'unzip' 'imagemagick' 'perl' 'ttf-droid' 'zip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bitwarden/clients/archive/refs/tags/browser-v${pkgver}.tar.gz")
b2sums=('f3fd9d7470d5ffb418716a8eea604c1f1967cd1a17f8543d0bae1fcd4b574b6ce44932012f3a01430a57c61ed8bd62fcd8390a96ac3279f29e45dd91ed88e30d')

prepare() {
  cd "${srcdir}/clients-browser-v${pkgver}" || exit
  # Starting in 2024.12.0 it uses powershell to create the zip. Rather than requiring powershell to build this, we just create the zip in the build step.
  sed -i 's| \&\& \.\/scripts\/compress\.ps1 dist-firefox\.zip||' apps/browser/package.json
  npm ci
}

build() {
  cd "${srcdir}/clients-browser-v${pkgver}/apps/browser" || exit
  find . -type f -exec perl -pi -e 's/"Bitwarden"/"Bitwarden - Matt"/go;' {} \;
  find . -type f -exec perl -pi -e 's/"Bitwarden - Free Password Manager"/"Bitwarden - Matt"/go;' {} \;
  find . -type f -exec perl -pi -e 's/"Bitwarden Password Manager"/"Bitwarden - Matt"/go;' {} \;
  find . -type f -exec perl -pi -e 's/446900e4-71c2-419f-a6a7-df9c091e268b/37c30bda-db14-4b06-aeb1-a1998de1a766/go;' {} \;
  find . -type f -exec perl -pi -e 's{<title>Bitwarden</title>}{<title>Bitwarden - Matt</title>}go;' {} \;
  for file in src/images/*; do
    # if file is svg, skip it
    if [[ $file == *.svg ]]; then
        continue
    fi

    # change color of the icon to green
    magick "$file" -modulate 100,70,60 "$file"
  done
  npm run dist:firefox
  cd build
  zip -r ../dist/dist-firefox.zip .
}

check() {
  cd "${srcdir}/clients-browser-v${pkgver}/apps/browser" || exit
  npm run test
}

package() {
  install -Dm644 "${srcdir}/clients-browser-v${pkgver}/apps/browser/dist/dist-firefox.zip" "${pkgdir}/usr/lib/firefox/browser/extensions/{37c30bda-db14-4b06-aeb1-a1998de1a766}.xpi"
}
