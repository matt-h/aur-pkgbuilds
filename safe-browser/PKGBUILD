# Maintainer: Bartłomiej Kamiński <bartlomiej.kaminski@maidsafe.net>
pkgname=safe-browser
pkgver=0.11.0
pkgrel=1
pkgdesc="A browser designed to open safe:// websites on the SAFE Network."
arch=('i686' 'x86_64')
url="https://github.com/maidsafe/safe_browser"
license=('MIT')
groups=('maidsafe')
depends=('gtk2' 'gconf' 'alsa-lib' 'libxtst' 'libxss')
makedepends=('nodejs-lts-carbon' 'yarn' 'unzip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/maidsafe/safe_browser/archive/$pkgver.tar.gz")
sha256sums=('2bffa2472bf3bc4f672a02972de0788cf1e966e99c863080ebcce1776f8ef345')
if [ "$CARCH" = "i686" ]; then
  _source_arch="x86"
else
  _source_arch="x64"
fi

build() {
  cd "$srcdir/safe_browser-$pkgver"
  yarn
  yarn run package
}

package() {
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  cp "$srcdir/safe_browser-$pkgver/LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  mkdir -p "$pkgdir/opt/maidsafe"
  mkdir -p "$pkgdir/usr/bin"
  mv "$srcdir/safe_browser-$pkgver/release/safe-browser-v$pkgver-linux-$_source_arch" "$pkgdir/opt/maidsafe/$pkgname-$pkgver"
  mkdir -p "$pkgdir/opt/maidsafe/$pkgname-$pkgver/resources/app/node_modules/locales"
  ln -s "/opt/maidsafe/$pkgname-$pkgver/safe-browser" "$pkgdir/usr/bin/safe-browser"
}
