# Maintainer: Sander Boom <sanderboom@gmail.com>
# Contributor: jarosz

pkgname=php-codesniffer
_pkgname=PHP_CodeSniffer
pkgver=3.9.1
pkgrel=1
pkgdesc="PHP_CodeSniffer tokenizes PHP, JavaScript and CSS files to detect and fix violations of a defined set of coding standards."
arch=('any')
url="https://github.com/PHPCSStandards/PHP_CodeSniffer"
license=('BSD-3-Clause')
depends=('php')
makedepends=('git')
_tag_commit=267a4405fff1d9c847134db3a3c92f1ab7f77909
source=("${pkgname}-${pkgver}::git+${url}#commit=$_tag_commit")
b2sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  php -d phar.readonly=0 ./scripts/build-phar.php
}

package() {
  install -Dm0755 "${srcdir}/${pkgname}-${pkgver}/phpcs.phar" "${pkgdir}/usr/bin/phpcs"
  install -Dm0755 "${srcdir}/${pkgname}-${pkgver}/phpcbf.phar" "${pkgdir}/usr/bin/phpcbf"

  install -d "${pkgdir}/usr/share/licenses"
  install -D -m 644 "${srcdir}/${pkgname}-${pkgver}/licence.txt" "${pkgdir}/usr/share/licenses/${pkgname}/licence.txt"
}
